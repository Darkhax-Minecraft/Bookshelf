buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
        classpath group: 'gradle.plugin.com.diluv.diluvgradle', name: 'DiluvGradle', version: '1.3.0'
        classpath group: 'com.diluv.schoomp', name: 'Schoomp', version: '1.1.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven-publish' 
apply plugin: 'eclipse'

apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/secrets.gradle'
apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'
apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/minecraft/artifacts.gradle'
apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/patreon.gradle'
//apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/minecraft/maven_local_nomcp.gradle'

import groovy.json.JsonSlurper
import groovy.json.JsonOutput
import com.diluv.schoomp.Webhook
import com.diluv.schoomp.message.Message
import com.diluv.schoomp.message.embed.Embed
import com.diluv.schoomp.message.embed.Footer

if (project.hasProperty('secretFile')) {
    loadSecrets(new File((String) findProperty('secretFile')))
}

version = "${mod_version}" + getBuildNumber()
archivesBaseName = "${mod_name}-${minecraft_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {

    mappings channel: 'snapshot', version: "${mcp_version}".toString()
    
    if (project.hasProperty('mod_at')) {
    
        accessTransformer = file(findProperty('mod_at'))
    }
    
    runs {
        client {
        
            workingDirectory project.file('run')
            taskName "${mod_name}".replaceAll("\\s","") + 'Client'
            property 'forge.logging.console.level', 'debug'
            
            mods {
                bookshelf {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            taskName "${mod_name}".replaceAll("\\s","") + 'Server'
            property 'forge.logging.console.level', 'debug'
            
            mods {
                bookshelf {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {

    minecraft "net.minecraftforge:forge:${forge_version}"
}

jar {
    manifest {
        attributes([
            'Timestamp': System.currentTimeMillis(),
            'Specification-Title': "${mod_name}",
            'Specification-Vendor': "${mod_vendor}",
            'Specification-Version': "${version}",
            'Implementation-Title': project.archivesBaseName,
            'Implementation-Version': project.version,
            'Implementation-Vendor' :"${mod_vendor}",
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'Built-On-Java': "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
            'Built-On': "${forge_version}"
        ])
    }
}

//Shuts up javadoc failures
if (JavaVersion.current().isJava8Compatible()) {

    allprojects {

        tasks.withType(Javadoc) {

            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

String getBuildNumber() {

    return System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : System.getenv('TRAVIS_BUILD_NUMBER') ? System.getenv('TRAVIS_BUILD_NUMBER') : '0';
}

processResources {

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        def credits = "Thanks to my supporters on Patreon. "

        def patreonSupporters = new ArrayList<String>()

        for (entry in getPledges()) {

            def pledge = entry.getValue();

            if (pledge.isValid()) {

                patreonSupporters.add(pledge.getDisplayName())
            }
        }

        credits += patreonSupporters.join(', ')

        expand 'version': project.version, 'mod_name': mod_name, 'mod_download': mod_download, 'mod_credits': credits, 'mod_authors': mod_authors, 'mod_description': mod_description
    }

    from(sourceSets.main.resources.srcDirs) {
    
        exclude 'META-INF/mods.toml'
    }
    
    doLast {
    
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
        
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

// CurseForge Publishing
curseforge {

    apiKey = findProperty('curse_auth') ?: 0 
    def versions = "${curse_versions}".split(', ')
    project {

        id = "${curse_project}"
        releaseType = 'alpha'
        changelog = getGitChangelog() + "\n\nHuge thanks to my supporters on [Patreon](https://www.patreon.com/Darkhax?MCChangelog?${mod_name})\n\n" + getPledgeLog()
        changelogType = 'markdown'

        versions.each {

            addGameVersion "${it}"
        }

        if (project.hasProperty('curse_requirements') || project.hasProperty('curse_optionals')) {

            mainArtifact(jar) {

                relations {

                    if (project.hasProperty('curse_requirements')) {
                        def requirements = "${curse_requirements}".split(', ')
                        requirements.each {

                            requiredLibrary "${it}"
                        }
                    }

                    if (project.hasProperty('curse_optionals')) {
                        def optionals = "${curse_optionals}".split(', ')
                        optionals.each {

                            optionalLibrary "${it}"
                        }
                    }
                }
            }
        }

        addArtifact(sourcesJar) {
            
            changelog = 'This Sources JAR file contains the raw source code for the mod. This file is intended for other mod developers who are curious or wish to learn from this mod. This file is not required to play with the mod and is often best to ignore.\n\n' + getGitChangelog()
        }
        
        addArtifact(javadocJar) {
            
            changelog = 'This Javadoc file contains documentation of the code for the mod. This file is intended for other mod developers who are curious or wish to learn from this mod. This file is not required to play with the mod and is often best to ignore.\n\n' + getGitChangelog()
        }
        
        addArtifact(deobfJar) {
            
            changelog = 'This DEOBF JAR file contains unobfuscated compiled code for the mod. This file is intended for other mod developers who want to run the mod in their development environment. This file is not required to play with the mod and is often best to ignore.\n\n' + getGitChangelog()
        }
    }
}

tasks.getByName("curseforge").doLast {
    
    try {
    
        // Reads the file ID given to us by CurseForge
        def newFileId = tasks.getByName("curseforge${curse_project}").property('mainArtifact').fileID
    
        def webhook = new Webhook(findProperty('discordCFWebhook'), "${project.findProperty('mod_name')} Gradle Upload");
        
        def message = new Message();        
        message.setUsername(findProperty('mod_name'));
        message.setContent("${project.findProperty('mod_name')} ${version} for Minecraft ${findProperty('minecraft_version')} has been released! The download will be public soon.");
        
        def embed = new Embed();
        embed.addField('Download', "${project.findProperty('curse_page')}/files/$newFileId", false);
        embed.addField('Change Log', "${getGitChangelog() ?: 'Unavailable :('}", false);
        embed.setColor(0xFF8000);           
        message.addEmbed(embed);

        webhook.sendMessage(message);
    }
    
    catch (IOException e) {
        println 'Failed to push to the Discord webhook.'
    }
}

// Update Version Tracker
task updateVersionTracker {

    onlyIf {
    
        project.hasProperty('versionTrackerAPI')
    }
    
    doLast {
        def body = [
            'author': "${project.findProperty('mod_vendor')}",
            'projectName': "${project.findProperty('mod_name').toLowerCase()}",
            'gameVersion': "${project.findProperty('minecraft_version')}",
            'projectVersion': "${version}",
            'homepage': "${project.findProperty('mod_download')}",
            'uid': "${project.findProperty('versionTrackerKey')}"
        ]
    
        // Opens a connection to the version tracker API and writes the payload JSON.
        def req = new URL(project.findProperty('versionTrackerAPI')).openConnection()
        req.setRequestMethod('POST')
        req.setRequestProperty('Content-Type', 'application/json; charset=UTF-8')
        req.setRequestProperty('User-Agent', "${project.findProperty('mod_name')} Tracker Gradle")
        req.setDoOutput(true)
        req.getOutputStream().write(JsonOutput.toJson(body).getBytes("UTF-8"))

        // We need to attempt a read in order to actually send the message.
        println "VersionCheck Status code: ${req.getResponseCode()}"
        println "VersionCheck Response: ${req.getInputStream().getText()}"
    }
}

// Diluv Publishing
import com.diluv.diluvgradle.TaskDiluvUpload

task publishDiluv (type: TaskDiluvUpload) {

    //failSilently = true;
    apiURL = findProperty('diluv_api')
    token = findProperty('diluv_token')
    changelog = getGitChangelog() + "\n\nHuge thanks to my supporters on [Patreon](https://www.patreon.com/Darkhax?MCChangelog?${mod_name})\n\n" + getPledgeLog()
    projectId = findProperty('diluv_project')
    projectVersion = project.version
    uploadFile = jar
    
    doLast {
    
        def webhook = new Webhook(findProperty('diluv_discord_webhook'), "${project.findProperty('mod_name')} Gradle Upload");
        def message = new Message();
        
        message.setUsername(findProperty('mod_name'));
            
        if (wasUploadSuccessful()) {
        
            message.setContent("${project.findProperty('mod_name')} ${version} for Minecraft ${findProperty('minecraft_version')} has been released! The download will be publicly usable soon.");
        
            def embed = new Embed();
            embed.addField('Download', "[**NodeCDN Download Link**](${uploadInfo.downloadURL})", false);
            embed.addField('Change Log', "${getGitChangelog() ?: 'Unavailable :('}", false);
            embed.setFooter(new Footer("Uploaded by ${uploadInfo.uploader.displayName}", uploadInfo.uploader.avatarURL));
            embed.setColor(0x009944);
            
            try {
                
                def connection = new URL("${apiURL}/v1/projects/${projectId}").openConnection() as HttpURLConnection;
                connection.setRequestProperty('User-Agent', 'Gradle Webhook');
                connection.setRequestProperty('Accept', 'application/json');
                
                def response = connection.inputStream.withCloseable {inStream -> new JsonSlurper().parse(inStream as InputStream)};
                
                if (connection.responseCode == 200) {
                    
                    message.setAvatarUrl(response.logo);
                }
            }
            
            catch (IOException e) {}
            
            message.addEmbed(embed);
        }
        
        else {
            
            message.setContent("There was an error uploading to Diluv :(");
            
            if (errorInfo != null) {
            
                def embed = new Embed();
                embed.addField("Project Version", version, true);
                embed.addField("Project ID", findProperty('diluv_project'), true);
                embed.addField("Type", errorInfo.type, false);
                embed.addField("Message", errorInfo.message, false);
                embed.setColor(0xcf000f);
                
                message.addEmbed(embed);
            }
        }
        
        webhook.sendMessage(message);
    }
}

// Jar Signing
import net.minecraftforge.gradle.common.task.SignJar

task signJar(type: SignJar, dependsOn: jar) {

    onlyIf {
    
        project.hasProperty('keyStore')
    }
    
    if (project.hasProperty('keyStore')) {
    
        project.logger.info('Signing jar ' + jar.archivePath.getName());
    
        keyStore = project.findProperty('keyStore')
        alias = project.findProperty('keyStoreAlias')
        storePass = project.findProperty('keyStorePass')
        keyPass = project.findProperty('keyStoreKeyPass')
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }

    else {
    
        project.logger.warn('Could not sign ' + jar.archivePath.getName() + '. No keyStore property could be found')
    }
}

build.dependsOn signJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Adds the sources as an artifact.
            artifact sourcesJar {
                classifier 'sources'
            }

            // Adds the javadocs as an artifact.
            artifact javadocJar {
                classifier 'javadoc'
            }

            // Adds the deobfuscated jar as an artifact.
            artifact deobfJar {
                classifier 'deobf'
            }
            
            artifact forgelibJar {
                classifier 'forgelib'
            }
        }
    }

    repositories {
        maven {
            // Sets the maven URL
            url findProperty('localMaven')
        }
    }
}