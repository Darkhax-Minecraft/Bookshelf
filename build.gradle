buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
        classpath "com.matthewprenger:CurseGradle:1.0.3-SNAPSHOT"
    }
}

apply plugin: "forge"
apply plugin: "com.matthewprenger.cursegradle"

version = "1.1"
group = "net.darkhax.bookshelf"
archivesBaseName = "Bookshelf"

minecraft {
    version = "1.7.10-10.13.4.1517-1.7.10"
    runDir = "run"
}

task buildInfo() {
    def cmd = "git rev-parse --short HEAD"
    def proc = cmd.execute()
    proc.waitFor()
    if (proc.exitValue() == 0) {
        ext.revision = "DEV." + proc.text.trim()
    } else {
        ext.revision = "GITBORK"
    }

    if (System.getenv().BUILD_NUMBER != null) {
        ext.buildNum = "${System.getenv().BUILD_NUMBER}"
    } else {
        ext.buildNum = "${project.buildInfo.revision}"
    }
}

import groovy.json.JsonSlurper
ext.versionFile = file "versions.json"
project.ext.versions = new JsonSlurper().parseText versionFile.text

ext.artifact_version = "NFG"
if (System.getenv().ARTIFACT_VERSION == null) {
    artifact_version = "${version}.${project.buildInfo.buildNum}"
}

if (System.getenv().ARTIFACT_VERSION != null) {
    artifact_version = "${system.getenv().ARTIFACT_VERSION}"
}

version = "1.7.10-${artifact_version}"

processResources {
    inputs.property "version", project.artifact_version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.artifact_version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = "deobf"
}

artifacts {
    archives deobfJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:///var/www/html/maven")
        }
    }
}

jar {
    manifest {
        attributes "FMLCorePlugin": "net.darkhax.bookshelf.asm.BookshelfLoadingPlugin", "FMLCorePluginContainsFMLMod": "true"
    }
}

curseforge {
    secretFile = file "secret.properties"
    secretFile.withReader {
        def prop = new Properties()
        prop.load(it)
        secret = new ConfigSlurper().parse prop
    }

    apiKey = secret.curseForgeApiKey

    project {
        id = secret.projectId
    
        releaseType = "release"
        changelog = versions.versions[versions.newestVersion].join("\n")

        addGameVersion "1.7.10"
        
        mainArtifact(jar) {
            displayName = "Bookshelf ${artifact_version} for Minecraft 1.7.10"
        }
        
        addArtifact(deobfJar) {
            displayName = "Bookshelf ${artifact_version} for Minecraft 1.7.10 (Deobf)"
        }
    }
}
